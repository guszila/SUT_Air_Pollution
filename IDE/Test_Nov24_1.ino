#include <WiFi.h>
#include <HTTPClient.h>
#include <WiFiClientSecure.h>
#include <DHT.h>
#include <HardwareSerial.h>
#include "time.h"

// ---------- DHT11 ----------
#define DHTPIN 15
#define DHTTYPE DHT11
DHT dht(DHTPIN, DHTTYPE);

float temp = 0;
float hum  = 0;

// ---------- PMS5003 ----------
#define RX 22
#define TX 21
HardwareSerial PMSSerial(2);

int pm1_0 = 0, pm2_5 = 0, pm10_0 = 0;

// ---------- WiFi ----------
const char* ssid = "FOCUS";
const char* password = "35984000";
//const char* ssid = "@SUT-IoT";
//const char* password = "";

// ---------- Google Script ----------
const char* scriptURL = "https://script.google.com/macros/s/AKfycbwEn4qtCXgDW150ufX307pNeNAO7qHSySh0y0J8aXSQaRbA2R5MQtbNUo9OJICWxioZ/exec";

// ---------- NTP ----------
const char* ntpServer = "pool.ntp.org";
const long gmtOffset_sec = 7 * 3600;
const int daylightOffset_sec = 0;

// ---------- Config ----------
const char* deviceName = "C_Test_Point";
//const char* deviceName = "A_Learning_Building_1"; 
//const char* deviceName = "B_Library_Building"; 

// ---------- LINE Token (Messaging API) ----------
const char* lineToken  = "r6HgHzteNFp6eZdFaJ7PVIAOewqv+jqzYCK+MpiHQqb6QVfB6fm7CYJ0PTOMQHEXXuqrO+MrMY6NCM1odCYP89TY6JiGuXtq6zh3ZrHygrXc5yatOgnMo0mMUsw6Dv9AEVRZaYmfOMtifhVyzG+szwdB04t89/1O/w1cDnyilFU=";

const char* lineUserId[] = {
  "Ub3910c430f940bcf723e72fe32ed16fd",
  // "Ubc722e5b5a82ae47608dbdfbacc87160"
};
const int userCount = 1;

// ==========================================
// [‡∏™‡∏≥‡∏Ñ‡∏±‡∏ç!] ‡∏ï‡∏±‡∏ß‡πÅ‡∏õ‡∏£‡∏™‡∏≥‡∏´‡∏£‡∏±‡∏ö Smart Alert (‡∏ï‡πâ‡∏≠‡∏á‡∏°‡∏µ‡∏ö‡∏£‡∏£‡∏ó‡∏±‡∏î‡∏ô‡∏µ‡πâ ‡πÑ‡∏°‡πà‡∏á‡∏±‡πâ‡∏ô Error)
// ==========================================
int lastLevel = -1; 

// ‡∏ü‡∏±‡∏á‡∏Å‡πå‡∏ä‡∏±‡∏ô‡∏Ñ‡∏≥‡∏ô‡∏ß‡∏ì‡∏£‡∏∞‡∏î‡∏±‡∏ö (‡πÄ‡∏Å‡∏ì‡∏ë‡πå‡πÄ‡∏î‡∏¥‡∏°: 36, 56, 151, 250)
int getAirLevel(int pm) {
  if (pm < 36) return 0;        // ‡∏õ‡∏Å‡∏ï‡∏¥
  if (pm <= 55) return 1;       // ‡πÄ‡∏£‡∏¥‡πà‡∏°‡∏≠‡∏±‡∏ô‡∏ï‡∏£‡∏≤‡∏¢
  if (pm <= 150) return 2;      // ‡∏≠‡∏±‡∏ô‡∏ï‡∏£‡∏≤‡∏¢‡∏°‡∏≤‡∏Å
  if (pm <= 250) return 3;      // ‡∏≠‡∏±‡∏ô‡∏ï‡∏£‡∏≤‡∏¢‡∏°‡∏≤‡∏Å‡∏ó‡∏µ‡πà‡∏™‡∏∏‡∏î
  return 4;                     // ‡∏ß‡∏¥‡∏Å‡∏§‡∏ï
}
// ==========================================


// ----------- URL Encode -----------
String urlencode(String str) {
  String encoded = "";
  char c;
  char code0;
  char code1;

  for (int i = 0; i < str.length(); i++) {
    c = str.charAt(i);
    if (isalnum(c)) encoded += c;
    else {
      code1 = (c & 0xf) + '0';
      if ((c & 0xf) > 9) code1 = (c & 0xf) - 10 + 'A';
      c = (c >> 4) & 0xf;
      code0 = c + '0';
      if (c > 9) code0 = c - 10 + 'A';
      encoded += '%';
      encoded += code0;
      encoded += code1;
    }
  }
  return encoded;
}


// ----------- ‡∏™‡πà‡∏á LINE (Messaging API) -----------
void sendLineAlert(String msg) {
  if (WiFi.status() != WL_CONNECTED) return;

  msg.replace("\n", "\\n");

  for (int i = 0; i < userCount; i++) {
    WiFiClientSecure client;
    client.setInsecure();

    HTTPClient https;
    String url = "https://api.line.me/v2/bot/message/push";

    if (!https.begin(client, url)) return;

    https.addHeader("Content-Type", "application/json");
    https.addHeader("Authorization", String("Bearer ") + lineToken);

    String payload =
      "{"
        "\"to\":\"" + String(lineUserId[i]) + "\","
        "\"messages\":["
          "{"
            "\"type\":\"text\","
            "\"text\":\"" + msg + "\""
          "}"
        "]"
      "}";

    https.POST(payload);
    https.end();
  }
}


void setup() {
  Serial.begin(115200);
  delay(1000);
  
  dht.begin();
  PMSSerial.begin(9600, SERIAL_8N1, RX, TX);

  // WiFi
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nWiFi Connected");

  // NTP
  configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);
}


// ----------- ‡∏≠‡πà‡∏≤‡∏ô PMS + DHT -----------
void readSensors() {
  unsigned long start = millis();
  while (PMSSerial.available() < 32) {
    if (millis() - start > 2000) return;
    delay(5);
  }

  if (PMSSerial.read() != 0x42) return;
  if (PMSSerial.read() != 0x4D) return;

  PMSSerial.read(); PMSSerial.read();

  uint16_t data[13];
  for (int i = 0; i < 13; i++) {
    data[i] = (PMSSerial.read() << 8) | PMSSerial.read();
  }

  pm1_0  = data[0];
  pm2_5  = data[1];
  pm10_0 = data[2];

  temp = dht.readTemperature();
  hum  = dht.readHumidity();

  if (isnan(temp) || isnan(hum)) return;

  struct tm timeinfo;
  getLocalTime(&timeinfo);
  char t[16];
  strftime(t, sizeof(t), "%H:%M:%S", &timeinfo);

  Serial.printf("[%s] PM1:%d PM2.5:%d PM10:%d Temp:%.1f Hum:%.1f\n",
                t, pm1_0, pm2_5, pm10_0, temp, hum);
}


// ----------- ‡∏™‡πà‡∏á‡∏Ç‡πâ‡∏≠‡∏°‡∏π‡∏•‡∏ó‡∏∏‡∏Å 90 ‡∏ß‡∏¥ -----------
void sendData() {
  struct tm timeinfo;
  getLocalTime(&timeinfo);

  char timeStr[16];
  strftime(timeStr, sizeof(timeStr), "%H:%M:%S", &timeinfo);

  // ============================================================
  // ‡πÅ‡∏à‡πâ‡∏á‡πÄ‡∏ï‡∏∑‡∏≠‡∏ô LINE 
  // ============================================================
  if (pm2_5 > 0) { 
    int currentLevel = getAirLevel(pm2_5);

    // ‡πÄ‡∏ä‡πá‡∏Ñ‡∏ß‡πà‡∏≤‡∏£‡∏∞‡∏î‡∏±‡∏ö‡πÄ‡∏õ‡∏•‡∏µ‡πà‡∏¢‡∏ô‡πÑ‡∏´‡∏°
    if (currentLevel != lastLevel) {
      
      String msg = "";

      // ‡∏Å‡∏£‡∏ì‡∏µ‡∏ó‡∏µ‡πà 1: ‡∏≠‡∏≤‡∏Å‡∏≤‡∏®‡πÅ‡∏¢‡πà‡∏•‡∏á 
      if (currentLevel > lastLevel) {
        if (currentLevel == 1) {
           msg = "‚ö†Ô∏è PM2.5 ‡πÄ‡∏£‡∏¥‡πà‡∏°‡∏≠‡∏±‡∏ô‡∏ï‡∏£‡∏≤‡∏¢ (" + String(pm2_5) + " ¬µg/m¬≥)\n"
                 "‡πÄ‡∏´‡∏°‡∏≤‡∏∞‡∏Å‡∏±‡∏ö‡∏Å‡∏≤‡∏£‡∏´‡∏•‡∏µ‡∏Å‡πÄ‡∏•‡∏µ‡πà‡∏¢‡∏á‡∏Å‡∏¥‡∏à‡∏Å‡∏£‡∏£‡∏°‡∏Å‡∏•‡∏≤‡∏á‡πÅ‡∏à‡πâ‡∏á";
        } 
        else if (currentLevel == 2) {
           msg = "‚ùó PM2.5 ‡∏≠‡∏±‡∏ô‡∏ï‡∏£‡∏≤‡∏¢‡∏°‡∏≤‡∏Å! (" + String(pm2_5) + " ¬µg/m¬≥)\n"
                 "‡∏Ñ‡∏ß‡∏£‡∏≠‡∏¢‡∏π‡πà‡πÉ‡∏ô‡∏≠‡∏≤‡∏Ñ‡∏≤‡∏£ ‡πÅ‡∏•‡∏∞‡∏™‡∏ß‡∏°‡∏´‡∏ô‡πâ‡∏≤‡∏Å‡∏≤‡∏Å‡πÄ‡∏°‡∏∑‡πà‡∏≠‡∏≠‡∏≠‡∏Å‡∏Ç‡πâ‡∏≤‡∏á‡∏ô‡∏≠‡∏Å";
        } 
        else if (currentLevel == 3) {
           msg = "üî¥ PM2.5 ‡∏≠‡∏±‡∏ô‡∏ï‡∏£‡∏≤‡∏¢‡∏°‡∏≤‡∏Å‡∏ó‡∏µ‡πà‡∏™‡∏∏‡∏î! (" + String(pm2_5) + " ¬µg/m¬≥)\n"
                 "‡∏°‡∏µ‡∏Ñ‡∏ß‡∏≤‡∏°‡πÄ‡∏™‡∏µ‡πà‡∏¢‡∏á‡∏ï‡πà‡∏≠‡∏õ‡∏≠‡∏î‡πÅ‡∏•‡∏∞‡∏´‡∏±‡∏ß‡πÉ‡∏à‡∏™‡∏π‡∏á ‡∏Ñ‡∏ß‡∏£‡πÄ‡∏•‡∏µ‡πà‡∏¢‡∏á‡∏≠‡∏≠‡∏Å‡∏ô‡∏≠‡∏Å‡∏≠‡∏≤‡∏Ñ‡∏≤‡∏£";
        }
        else if (currentLevel == 4) {
           msg = "üö® ‡∏†‡∏≤‡∏ß‡∏∞‡∏ß‡∏¥‡∏Å‡∏§‡∏ï‡∏¥ PM2.5 > 250 (" + String(pm2_5) + " ¬µg/m¬≥)\n"
                 "‡πÑ‡∏°‡πà‡∏Ñ‡∏ß‡∏£‡∏≠‡∏≠‡∏Å‡∏ô‡∏≠‡∏Å‡∏≠‡∏≤‡∏Ñ‡∏≤‡∏£‡πÄ‡∏î‡πá‡∏î‡∏Ç‡∏≤‡∏î!";
        }
      }
      
      // ‡∏Å‡∏£‡∏ì‡∏µ‡∏ó‡∏µ‡πà 2: ‡∏≠‡∏≤‡∏Å‡∏≤‡∏®‡∏î‡∏µ‡∏Ç‡∏∂‡πâ‡∏ô (‡∏Å‡∏•‡∏±‡∏ö‡∏°‡∏≤‡πÄ‡∏õ‡πá‡∏ô‡∏™‡∏µ‡πÄ‡∏Ç‡∏µ‡∏¢‡∏ß)
      else if (currentLevel == 0 && lastLevel > 0) {
         msg = "‚úÖ ‡∏Ñ‡∏∏‡∏ì‡∏†‡∏≤‡∏û‡∏≠‡∏≤‡∏Å‡∏≤‡∏®‡∏Å‡∏•‡∏±‡∏ö‡∏°‡∏≤‡∏î‡∏µ‡πÅ‡∏•‡πâ‡∏ß (" + String(pm2_5) + " ¬µg/m¬≥)\n"
               "‡∏õ‡∏•‡∏≠‡∏î‡∏†‡∏±‡∏¢‡∏Ñ‡∏£‡∏±‡∏ö";
      }

      // ‡∏™‡πà‡∏á‡∏Ç‡πâ‡∏≠‡∏Ñ‡∏ß‡∏≤‡∏°
      if (msg != "") {
        sendLineAlert(msg);
        lastLevel = currentLevel; // ‡∏à‡∏≥‡∏Ñ‡πà‡∏≤‡πÑ‡∏ß‡πâ
      }
    }
  }
  // ============================================================

  // ‡∏™‡∏£‡πâ‡∏≤‡∏á URL ‡∏™‡πà‡∏á Google Sheet
  String url = String(scriptURL) + 
               "?device=" + deviceName +
               "&pm1=" + String(pm1_0) +
               "&pm2_5=" + String(pm2_5) +
               "&pm10=" + String(pm10_0) +
               "&temp=" + String(temp) + 
               "&hum=" + String(hum) +
               "&esptime=" + urlencode(timeStr);

  WiFiClientSecure client;
  client.setInsecure();

  HTTPClient https;
  https.begin(client, url);

  int httpCode = https.GET();
  Serial.println("HTTP: " + String(httpCode));

  https.end();
}


// ---------- Loop -----------
unsigned long lastRead = 0;
unsigned long lastSend = 0;

void loop() {
  unsigned long now = millis();

  // ‡∏≠‡πà‡∏≤‡∏ô‡∏ó‡∏∏‡∏Å 1 ‡∏ß‡∏¥
  if (now - lastRead >= 1000) {
    readSensors();
    lastRead = now;
  }

  // ‡∏™‡πà‡∏á‡∏ó‡∏∏‡∏Å 90 ‡∏ß‡∏¥
  if (now - lastSend >= 90000) {
    sendData();
    lastSend = now;
    Serial.println("-------------------------");
  }
}